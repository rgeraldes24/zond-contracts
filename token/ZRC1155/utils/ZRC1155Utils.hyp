// SPDX-License-Identifier: MIT
// Zond Contracts (last updated v0.1.0) (token/ZRC1155/utils/ZRC1155Utils.hyp)

pragma hyperion >=0.1.0;

import {IZRC1155Receiver} from "../IZRC1155Receiver.hyp";
import {IZRC1155Errors} from "../../../interfaces/draft-IZRC6093.hyp";

/**
 * @dev Library that provide common ZRC-1155 utility functions.
 *
 * See https://eips.ethereum.org/EIPS/eip-1155[ZRC-1155].
 *
 * _Available since v5.1._
 */
library ZRC1155Utils {
    /**
     * @dev Performs an acceptance check for the provided `operator` by calling {IZRC1155Receiver-onZRC1155Received}
     * on the `to` address. The `operator` is generally the address that initiated the token transfer (i.e. `msg.sender`).
     *
     * The acceptance call is not executed and treated as a no-op if the target address doesn't contain code (i.e. an EOA).
     * Otherwise, the recipient must implement {IZRC1155Receiver-onZRC1155Received} and return the acceptance magic value to accept
     * the transfer.
     */
    function checkOnZRC1155Received(
        address operator,
        address from,
        address to,
        uint256 id,
        uint256 value,
        bytes memory data
    ) internal {
        if (to.code.length > 0) {
            try IZRC1155Receiver(to).onZRC1155Received(operator, from, id, value, data) returns (bytes4 response) {
                if (response != IZRC1155Receiver.onZRC1155Received.selector) {
                    // Tokens rejected
                    revert IZRC1155Errors.ZRC1155InvalidReceiver(to);
                }
            } catch (bytes memory reason) {
                if (reason.length == 0) {
                    // non-IZRC1155Receiver implementer
                    revert IZRC1155Errors.ZRC1155InvalidReceiver(to);
                } else {
                    assembly ("memory-safe") {
                        revert(add(32, reason), mload(reason))
                    }
                }
            }
        }
    }

    /**
     * @dev Performs a batch acceptance check for the provided `operator` by calling {IZRC1155Receiver-onZRC1155BatchReceived}
     * on the `to` address. The `operator` is generally the address that initiated the token transfer (i.e. `msg.sender`).
     *
     * The acceptance call is not executed and treated as a no-op if the target address doesn't contain code (i.e. an EOA).
     * Otherwise, the recipient must implement {IZRC1155Receiver-onZRC1155Received} and return the acceptance magic value to accept
     * the transfer.
     */
    function checkOnZRC1155BatchReceived(
        address operator,
        address from,
        address to,
        uint256[] memory ids,
        uint256[] memory values,
        bytes memory data
    ) internal {
        if (to.code.length > 0) {
            try IZRC1155Receiver(to).onZRC1155BatchReceived(operator, from, ids, values, data) returns (
                bytes4 response
            ) {
                if (response != IZRC1155Receiver.onZRC1155BatchReceived.selector) {
                    // Tokens rejected
                    revert IZRC1155Errors.ZRC1155InvalidReceiver(to);
                }
            } catch (bytes memory reason) {
                if (reason.length == 0) {
                    // non-IZRC1155Receiver implementer
                    revert IZRC1155Errors.ZRC1155InvalidReceiver(to);
                } else {
                    assembly ("memory-safe") {
                        revert(add(32, reason), mload(reason))
                    }
                }
            }
        }
    }
}
